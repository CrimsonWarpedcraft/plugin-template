import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.5.0"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = "com.snowypeaksystems.exampleplugin"

if (hasProperty('ver')) {
    if (ver.charAt(0) == "v") {
        version = ver.substring(1)
    } else {
        version = ver
    }
} else {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    version = sdf.format(new Date()).toString()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    maven {
        name 'papermc'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.2'
    implementation 'io.papermc:paperlib:1.0.5'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.2'
    testImplementation 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

processResources {
    filesMatching("**/plugin.yml") {
        expand ( NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString() )
    }
}

checkstyle {
    toolVersion '8.35'
    maxWarnings = 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled false
    }
}

shadowJar {
    archiveClassifier.set('SNAPSHOT')
    relocate 'io.papermc.lib', 'shadow.io.papermc.lib'
    minimize()
}

jar.enabled = false
assemble.dependsOn(shadowJar)

task release {
    dependsOn build
    doLast {
        def dirStr = buildDir.toString() + '/libs/'

        file(dirStr + rootProject.name + '-' + version + '-SNAPSHOT.jar').renameTo(dirStr + rootProject.name + '.jar')
    }
}
