import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.5.0"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = "com.snowypeaksystems.exampleplugin"

if (hasProperty('ver')) {
    if (ver.charAt(0) == "v") {
        version = ver.substring(1)
    } else {
        version = ver
    }
} else {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    version = sdf.format(new Date()).toString()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        name 'papermc'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    compileOnly group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.2-R0.1-SNAPSHOT'
    implementation group: 'io.papermc', name: 'paperlib', version: '1.0.5'
    testImplementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version:'3.1.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.2-R0.1-SNAPSHOT'
}

processResources {
    filesMatching("**/plugin.yml") {
        expand ( NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString() )
    }
}

checkstyle {
    toolVersion '8.35'
    maxWarnings = 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled false
    }
}

shadowJar {
    archiveClassifier.set('SNAPSHOT')
    relocate 'io.papermc.lib', 'shadow.io.papermc.lib'
    minimize()
}

jar.enabled = false
assemble.dependsOn(shadowJar)

task release {
    dependsOn build
    doLast {
        def dirStr = buildDir.toString() + '/libs/'

        file(dirStr + rootProject.name + '-' + version + '-SNAPSHOT.jar').renameTo(dirStr + rootProject.name + '.jar')
    }
}
